// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 23
        compileSdkVersion = 33
        targetSdkVersion = 33

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
    }
}

ext.getConfig = { String name ->
    File localPropertiesFile = project.file('local.properties')
    if (localPropertiesFile?.exists()) {
        Properties localProperties = new Properties()
        localProperties.load(localPropertiesFile.newDataInputStream())
        if (localProperties.containsKey(name)) {
            return localProperties.getProperty(name)
        }
    }
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }
    def prop = System.getProperty(name)
    if (prop != null) {
        return prop
    }
    def env = System.getenv(name)
    if (env != null) {
        return env
    }

    println("Getting env variable failed, returning empty: set $name as environment variable or as system property in your ~/.gradle/gradle.properties")
    return ""
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.github.com/nevissecurity/nevis-mobile-authentication-sdk-android-package"
            credentials {
                username = getConfig("GITHUB_USERNAME")
                password = getConfig("GITHUB_PERSONAL_ACCESS_TOKEN")
            }
        }
    }
}
