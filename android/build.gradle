buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 34
        ndkVersion = "26.1.10909125"
        kotlinVersion = "1.9.22"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

apply plugin: "com.facebook.react.rootproject"

ext.getConfig = { String name ->
    File localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile?.exists()) {
        Properties localProperties = new Properties()
        localProperties.load(localPropertiesFile.newDataInputStream())
        if (localProperties.containsKey(name)) {
            return localProperties.getProperty(name)
        }
    }
    def env = System.getenv(name)
    if (env != null) {
        return env
    }
    def prop = System.getProperty(name)
    if (prop != null) {
        return prop
    }
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }

    println("Getting env variable failed, returning empty: set $name as environment variable or as system property in your ~/.gradle/gradle.properties")
    return ""
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.github.com/nevissecurity/nevis-mobile-authentication-sdk-android-package"
            credentials {
                username = getConfig("GITHUB_USERNAME")
                password = getConfig("GITHUB_PERSONAL_ACCESS_TOKEN")
            }
        }
    }
}

// Workaround for a React Native issue, should remain until it is solved: https://github.com/facebook/react-native/issues/44501
gradle.startParameter.excludedTaskNames.addAll(
    gradle.startParameter.taskNames.findAll { it.contains("testClasses") }
)
